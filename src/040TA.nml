template tmpl_vehicle_040TA_8_views(frame, livery) {
    //xpos ypos xsize ysize xrel yrel
    [5 + livery % 2 * 208, 5 + frame * 29 + int(livery / 2) * 116, 8, 24, -3, -12]
    [18 + livery % 2 * 208, 5 + frame * 29 + int(livery / 2) * 116, 22, 17, -14, -9]
    [45 + livery % 2 * 208, 5 + frame * 29 + int(livery / 2) * 116, 32, 14, -16, -10]
    [82 + livery % 2 * 208, 5 + frame * 29 + int(livery / 2) * 116, 22, 17, -1, -7]
    [109 + livery % 2 * 208, 5 + frame * 29 + int(livery / 2) * 116, 8, 24,  -3, -9]
    [122 + livery % 2 * 208, 5 + frame * 29 + int(livery / 2) * 116, 22, 17, -20, -8]
    [149 + livery % 2 * 208, 5 + frame * 29 + int(livery / 2) * 116, 32, 14, -16, -9]
    [186 + livery % 2 * 208, 5 + frame * 29 + int(livery / 2) * 116, 22, 17, -8, -9]
}

template tmpl_vehicle_040TA_purchase() {
    //xpos ypos xsize ysize xrel yrel
    [149, 5, 32, 14, 5, -9]
}

spriteset(set_040TA_graphics_0_0,      "gfx/040TA.png") { tmpl_vehicle_040TA_8_views(0, 0) }
spriteset(set_040TA_graphics_1_0,      "gfx/040TA.png") { tmpl_vehicle_040TA_8_views(1, 0) }
spriteset(set_040TA_graphics_2_0,      "gfx/040TA.png") { tmpl_vehicle_040TA_8_views(2, 0) }
spriteset(set_040TA_graphics_3_0,      "gfx/040TA.png") { tmpl_vehicle_040TA_8_views(3, 0) }
spriteset(set_040TA_graphics_0_1,      "gfx/040TA.png") { tmpl_vehicle_040TA_8_views(0, 1) }
spriteset(set_040TA_graphics_1_1,      "gfx/040TA.png") { tmpl_vehicle_040TA_8_views(1, 1) }
spriteset(set_040TA_graphics_2_1,      "gfx/040TA.png") { tmpl_vehicle_040TA_8_views(2, 1) }
spriteset(set_040TA_graphics_3_1,      "gfx/040TA.png") { tmpl_vehicle_040TA_8_views(3, 1) }
spriteset(set_040TA_graphics_0_2,      "gfx/040TA.png") { tmpl_vehicle_040TA_8_views(0, 2) }
spriteset(set_040TA_graphics_1_2,      "gfx/040TA.png") { tmpl_vehicle_040TA_8_views(1, 2) }
spriteset(set_040TA_graphics_2_2,      "gfx/040TA.png") { tmpl_vehicle_040TA_8_views(2, 2) }
spriteset(set_040TA_graphics_3_2,      "gfx/040TA.png") { tmpl_vehicle_040TA_8_views(3, 2) }
spriteset(set_040TA_graphics_0_3,      "gfx/040TA.png") { tmpl_vehicle_040TA_8_views(0, 3) }
spriteset(set_040TA_graphics_1_3,      "gfx/040TA.png") { tmpl_vehicle_040TA_8_views(1, 3) }
spriteset(set_040TA_graphics_2_3,      "gfx/040TA.png") { tmpl_vehicle_040TA_8_views(2, 3) }
spriteset(set_040TA_graphics_3_3,      "gfx/040TA.png") { tmpl_vehicle_040TA_8_views(3, 3) }
spriteset(set_040TA_graphics_purchase, "gfx/040TA.png") { tmpl_vehicle_040TA_purchase() }

switch(FEAT_TRAINS, SELF, sw_040TA_graphics_0, motion_counter % 4) {
    0: set_040TA_graphics_0_0;
    1: set_040TA_graphics_1_0;
    2: set_040TA_graphics_2_0;
    3: set_040TA_graphics_3_0;
}

switch(FEAT_TRAINS, SELF, sw_040TA_graphics_1, motion_counter % 4) {
    0: set_040TA_graphics_0_1;
    1: set_040TA_graphics_1_1;
    2: set_040TA_graphics_2_1;
    3: set_040TA_graphics_3_1;
}

switch(FEAT_TRAINS, SELF, sw_040TA_graphics_2, motion_counter % 4) {
    0: set_040TA_graphics_0_2;
    1: set_040TA_graphics_1_2;
    2: set_040TA_graphics_2_2;
    3: set_040TA_graphics_3_2;
}

switch(FEAT_TRAINS, SELF, sw_040TA_graphics_3, motion_counter % 4) {
    0: set_040TA_graphics_0_3;
    1: set_040TA_graphics_1_3;
    2: set_040TA_graphics_2_3;
    3: set_040TA_graphics_3_3;
}

random_switch(FEAT_TRAINS, SELF, sw_040TA_graphics_random) {
    1: sw_040TA_graphics_0;
    2: sw_040TA_graphics_1;
    3: sw_040TA_graphics_2;
    4: sw_040TA_graphics_3;
}

item(FEAT_TRAINS, item_040TA) {
    property {
        name:                         string(STR_SNCF_TRAIN_040TA_NAME);
        climates_available:           (param_allow_steam) ? bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL) : NO_CLIMATE;
        introduction_date:            (param_introduction_dates) ? date(1914, 1, 1) : date(1901, 1, 1);
        model_life:                   48;
        vehicle_life:                 18;
        reliability_decay:            9;
        refittable_cargo_classes:     NO_CARGO_CLASS;
        non_refittable_cargo_classes: ALL_CARGO_CLASSES;
        loading_speed:                LOADING_NONE; 
        cost_factor:                  7;
        running_cost_factor:          40;
        sprite_id:                    SPRITE_ID_NEW_TRAIN;
        speed:                        45 km/h;
        misc_flags:                   bitmask();
        refit_cost:                   0;
        track_type:                   UNELECTRIFIED;
        ai_special_flag:              AI_FLAG_PASSENGER | AI_FLAG_CARGO;
        power:                        500 hp;
        running_cost_base:            RUNNING_COST_STEAM;
        dual_headed:                  0;
        cargo_capacity:               0;
        weight:                       60 ton;
        ai_engine_rank:               0;
        engine_class:                 ENGINE_CLASS_STEAM;
        extra_power_per_wagon:        0 kW;
        tractive_effort_coefficient:  0.3;
        air_drag_coefficient:         0.06;
        visual_effect_and_powered:    visual_effect_and_powered(VISUAL_EFFECT_STEAM, -4, DISABLE_WAGON_POWER);
        extra_weight_per_wagon:       0 ton;
        bitmask_vehicle_info:         0;
        length:                       5;
    }
    graphics {
        default:                      sw_040TA_graphics_random;
        purchase:                     set_040TA_graphics_purchase;
        additional_text:              return string(STR_SNCF_TRAIN_040TA_ADDITIONAL_TEXT);
    }
}