template tmpl_vehicle_T2000_front() {
    //xpos ypos xsize ysize xrel yrel
    [3, 4, 8, 24, -3, -12]
    [18, 4, 22, 17, -15, -6]
    [49, 4, 32, 13, -17, -8]
    [90, 4, 22, 17, -4, -7]
    [118, 4, 8, 24, -3, -11]
    [133, 4, 22, 17, -16, -6]
    [164, 4, 32, 13, -18, -8]
    [202, 4, 22, 17, -4, -7]
}

template tmpl_vehicle_T2000_rear() {
    //xpos ypos xsize ysize xrel yrel
    [118, 36, 8, 24, -3, -11]
    [133, 36, 22, 17, -16, -6]
    [164, 36, 32, 13, -18, -8]
    [202, 36, 22, 17, -4, -7]
    [3, 36, 8, 24, -3, -12]
    [18, 36, 22, 17, -15, -6]
    [49, 36, 32, 13, -17, -8]
    [90, 36, 22, 17, -4, -7]
}

template tmpl_vehicle_T2000_4_views_coach() {
    //xpos ypos xsize ysize xrel yrel
    [3, 67, 8, 24, -3, -12]
    [18, 67, 22, 17, -15, -6]
    [49, 67, 32, 12, -17, -7]
    [90, 67, 22, 17, -4, -7]
}

template tmpl_vehicle_T2000_purchase() {
    //xpos ypos xsize ysize xrel yrel
    [164, 4, 32, 13, -7, -9]
}

spriteset(set_T2000_graphics_front,    "gfx/T2000.png") { tmpl_vehicle_T2000_front() }
spriteset(set_T2000_graphics_coach,    "gfx/T2000.png") { tmpl_vehicle_T2000_4_views_coach() }
spriteset(set_T2000_graphics_rear,     "gfx/T2000.png") { tmpl_vehicle_T2000_rear() }
spriteset(set_T2000_graphics_purchase, "gfx/T2000.png") { tmpl_vehicle_T2000_purchase() }

switch(FEAT_TRAINS, SELF, sw_T2000_graphics, position_in_articulated_veh % 5) {
    0: set_T2000_graphics_front;
    1: set_T2000_graphics_coach;
    2: set_T2000_graphics_coach;
    3: set_T2000_graphics_coach;
    4: set_T2000_graphics_rear;
}

switch(FEAT_TRAINS, SELF, sw_T2000_articulated_part, extra_callback_info1) {
    1 .. 4 : return item_T2000;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch(FEAT_TRAINS, SELF, sw_T2000_check_length, num_vehs_in_consist) {
    0 .. 10: return CB_RESULT_NO_TEXT;
    return string(ERR_T2000_TOO_LONG);
}

switch(FEAT_TRAINS, SELF, sw_T2000_can_attach_T2000, vehicle_type_id) {
    item_T2000 : CB_RESULT_ATTACH_ALLOW;
    default: return string(ERR_T2000_CANNOT_ATTACH_OTHER);
}

switch(FEAT_TRAINS, SELF, sw_T2000_can_attach_wagon, build_year) {
    0 .. 1985 : return string(ERR_T2000_CANNOT_ATTACH_OTHER);
    default: sw_T2000_can_attach_T2000;
}

switch(FEAT_TRAINS, SELF, sw_T2000_visual_effect_and_powered, position_in_articulated_veh) {
    0: visual_effect_and_powered(VISUAL_EFFECT_DIESEL, -3, DISABLE_WAGON_POWER);
    4: visual_effect_and_powered(VISUAL_EFFECT_DIESEL, 1, DISABLE_WAGON_POWER);
    default: visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
}

item(FEAT_TRAINS, item_T2000) {
    property {
        name:                         string(STR_SNCF_TRAIN_T2000_NAME);
        climates_available:           (param_allow_diesel) ? bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL) : NO_CLIMATE;
        introduction_date:            date(1973, 1, 1);
        model_life:                   44;
        vehicle_life:                 20;
        reliability_decay:            11;
        refittable_cargo_classes:     bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes: bitmask();
        loading_speed:                LOADING_FAST; 
        cost_factor:                  26;
        running_cost_factor:          65;
        sprite_id:                    SPRITE_ID_NEW_TRAIN;
        speed:                        225 km/h;
        misc_flags:                   bitmask(TRAIN_FLAG_MU, TRAIN_FLAG_AUTOREFIT);
        refit_cost:                   0;
        track_type:                   UNELECTRIFIED;
        ai_special_flag:              AI_FLAG_PASSENGER;
        power:                        3220 hp;
        running_cost_base:            RUNNING_COST_DIESEL;
        dual_headed:                  0;
        cargo_capacity:               64; // 320 total
        weight:                       225 ton;
        ai_engine_rank:               0;
        engine_class:                 ENGINE_CLASS_DIESEL;
        extra_power_per_wagon:        0 kW;
        tractive_effort_coefficient:  0.4;
        air_drag_coefficient:         0.06;
        extra_weight_per_wagon:       0 ton;
        bitmask_vehicle_info:         0;
        length:                       8;
    }
    graphics {
        default:                      sw_T2000_graphics;
        purchase:                     set_T2000_graphics_purchase;
        additional_text:              return string(STR_SNCF_TRAIN_T2000_ADDITIONAL_TEXT);
        articulated_part:             sw_T2000_articulated_part;
        visual_effect_and_powered:    sw_T2000_visual_effect_and_powered;
        can_attach_wagon:             sw_T2000_can_attach_wagon;
        start_stop:                   sw_T2000_check_length;
    }
}
