template tmpl_vehicle_1ABBA1_8_views_front(livery) {
    //xpos ypos xsize ysize xrel yrel
    [3, 2 + livery * 56, 8, 24, -3, -23]
    [13, 2 + livery * 56, 23, 20, -18, -7]
    [38, 2 + livery * 56, 32, 15, -16, -11]
    [72, 2 + livery * 56, 23, 20, -4, -10]
    [97, 2 + livery * 56, 8, 24, -3, -14]
    [107, 2 + livery * 56, 23, 20, -19, -9]
    [132, 2 + livery * 56, 32, 15, -17, -11]
    [166, 2 + livery * 56, 23, 20, -5, -8]
}

template tmpl_vehicle_1ABBA1_8_views_rear(livery) {
    //xpos ypos xsize ysize xrel yrel
    [3, 28 + livery * 56, 8, 24, -3, -15]
    [13, 28 + livery * 56, 23, 20, -18, -6]
    [38, 28 + livery * 56, 32, 15, -1, -11]
    [72, 28 + livery * 56, 23, 20, -5, -11]
    [97, 28 + livery * 56, 8, 24, -3, -7]
    [107, 28 + livery * 56, 23, 20, -18, -10]
    [132, 28 + livery * 56, 32, 15, -32, -11]
    [166, 28 + livery * 56, 23, 20, -5, -7]
}

template tmpl_vehicle_1ABBA1_purchase() {
    //xpos ypos xsize ysize xrel yrel
    [142, 2, 32, 15, -29, -11]
}

spriteset(set_1ABBA1_graphics_front_0,  "gfx/1ABBA1.png") { tmpl_vehicle_1ABBA1_8_views_front(0) }
spriteset(set_1ABBA1_graphics_rear_0,   "gfx/1ABBA1.png") { tmpl_vehicle_1ABBA1_8_views_rear(0) }
spriteset(set_1ABBA1_graphics_front_1,  "gfx/1ABBA1.png") { tmpl_vehicle_1ABBA1_8_views_front(1) }
spriteset(set_1ABBA1_graphics_rear_1,   "gfx/1ABBA1.png") { tmpl_vehicle_1ABBA1_8_views_rear(1) }
spriteset(set_1ABBA1_graphics_front_2,  "gfx/1ABBA1.png") { tmpl_vehicle_1ABBA1_8_views_front(2) }
spriteset(set_1ABBA1_graphics_rear_2,   "gfx/1ABBA1.png") { tmpl_vehicle_1ABBA1_8_views_rear(2) }
spriteset(set_1ABBA1_graphics_purchase, "gfx/1ABBA1_mockup.png") { tmpl_vehicle_1ABBA1_purchase() }

switch(FEAT_TRAINS, SELF, sw_1ABBA1_graphics_set_0, position_in_articulated_veh) {
    0: set_1ABBA1_graphics_front_0;
    1: set_1ABBA1_graphics_rear_0;
}

switch(FEAT_TRAINS, SELF, sw_1ABBA1_graphics_set_1, position_in_articulated_veh) {
    0: set_1ABBA1_graphics_front_1;
    1: set_1ABBA1_graphics_rear_1;
}

switch(FEAT_TRAINS, SELF, sw_1ABBA1_graphics_set_2, position_in_articulated_veh) {
    0: set_1ABBA1_graphics_front_2;
    1: set_1ABBA1_graphics_rear_2;
}

random_switch(FEAT_TRAINS, SELF, sw_1ABBA1_graphics) {
    1: sw_1ABBA1_graphics_set_0;
    2: sw_1ABBA1_graphics_set_1;
    3: sw_1ABBA1_graphics_set_2;
}

switch(FEAT_TRAINS, SELF, sw_1ABBA1_articulated_part, extra_callback_info1) {
    1 : return item_1ABBA1;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

item(FEAT_TRAINS, item_1ABBA1) {
    property {
        name:                         string(STR_SNCF_TRAIN_1ABBA1_NAME);
        climates_available:           (param_allow_electric) ? bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL) : NO_CLIMATE;
        introduction_date:            date(1926, 1, 1);
        model_life:                   47;
        vehicle_life:                 45;
        reliability_decay:            17;
        refittable_cargo_classes:     NO_CARGO_CLASS;
        non_refittable_cargo_classes: ALL_CARGO_CLASSES;
        loading_speed:                LOADING_NONE; 
        cost_factor:                  32;
        running_cost_factor:          76;
        sprite_id:                    SPRITE_ID_NEW_TRAIN;
        speed:                        80 km/h;
        misc_flags:                   bitmask();
        refit_cost:                   0;
        //track_type:                   (!param_allow_voltage) ? ELEC : DC15;
        track_type:                   DC15;
        ai_special_flag:              AI_FLAG_PASSENGER;
        power:                        2330 hp;
        running_cost_base:            RUNNING_COST_ELECTRIC;
        dual_headed:                  0;
        cargo_capacity:               0;
        weight:                       130 ton;
        ai_engine_rank:               0;
        engine_class:                 ENGINE_CLASS_ELECTRIC;
        extra_power_per_wagon:        0 kW;
        tractive_effort_coefficient:  0.3;
        air_drag_coefficient:         0.06;
        extra_weight_per_wagon:       0 ton;
        bitmask_vehicle_info:         0;
        length:                       4;
    }
    graphics {
        default:                      sw_1ABBA1_graphics;
        purchase:                     set_1ABBA1_graphics_purchase;
        additional_text:              return string(STR_SNCF_TRAIN_1ABBA1_ADDITIONAL_TEXT);
        articulated_part:             sw_1ABBA1_articulated_part;
        visual_effect_and_powered:    return (position_in_articulated_veh == 0) ?
                                          visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, -3, DISABLE_WAGON_POWER) :
                                          visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
    }
}
