switch(FEAT_TRAINS, SELF, sw_140C_graphics_front, param_allow_no_graphics) {
    0: set_vehicle_DUMMY_PloddyPhut;
    1: set_vehicle_DUMMY_6LEN;
}

switch(FEAT_TRAINS, SELF, sw_140C_graphics_purchase, param_allow_no_graphics) {
    0: set_vehicle_DUMMY_purchase_PloddyPhut;
    1: set_vehicle_DUMMY_purchase;
}

switch(FEAT_TRAINS, SELF, sw_140C_graphics, position_in_articulated_veh) {
    0: sw_140C_graphics_front;
    1: set_vehicle_DUMMY_3LEN;
}

switch(FEAT_TRAINS, SELF, sw_140C_articulated_part, extra_callback_info1) {
    1 : return item_140C;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

item(FEAT_TRAINS, item_140C) {
    property {
        name:                         string(STR_SNCF_TRAIN_140C_NAME);
        climates_available:           (param_allow_no_graphics < 2 && param_allow_steam) ? bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL) : NO_CLIMATE;
        introduction_date:            (param_introduction_dates) ? date(1913, 1, 1) : date(1905, 1, 1);
        model_life:                   50;
        vehicle_life:                 29;
        reliability_decay:            7;
        refittable_cargo_classes:     NO_CARGO_CLASS;
        non_refittable_cargo_classes: ALL_CARGO_CLASSES;
        loading_speed:                LOADING_NONE; 
        cost_factor:                  17;
        running_cost_factor:          48;
        sprite_id:                    SPRITE_ID_NEW_TRAIN;
        speed:                        80 km/h;
        misc_flags:                   bitmask();
        refit_cost:                   0;
        track_type:                   UNELECTRIFIED;
        ai_special_flag:              AI_FLAG_PASSENGER | AI_FLAG_CARGO;
        power:                        1500 hp;
        running_cost_base:            RUNNING_COST_STEAM;
        dual_headed:                  0;
        cargo_capacity:               0;
        weight:                       126.4 ton;
        ai_engine_rank:               0;
        engine_class:                 ENGINE_CLASS_STEAM;
        extra_power_per_wagon:        0 kW;
        tractive_effort_coefficient:  0.3;
        air_drag_coefficient:         0.06;
        visual_effect_and_powered:    visual_effect_and_powered(VISUAL_EFFECT_STEAM, 2, DISABLE_WAGON_POWER);
        extra_weight_per_wagon:       0 ton;
        bitmask_vehicle_info:         0;
    }
    graphics {
        default:                      sw_140C_graphics;
        purchase:                     sw_140C_graphics_purchase;
        additional_text:              return string(STR_SNCF_TRAIN_140C_ADDITIONAL_TEXT);
        articulated_part:             sw_140C_articulated_part;
        visual_effect_and_powered:    return (position_in_articulated_veh == 0) ?
                                          visual_effect_and_powered(VISUAL_EFFECT_STEAM, -4, DISABLE_WAGON_POWER) :
                                          visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
        length:                       return (position_in_articulated_veh == 0) ? 6 : 3;
    }
}
